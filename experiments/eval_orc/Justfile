root := `git rev-parse --show-toplevel`
eval_cmd := "uv run paper gpt eval"

# Configuration selector
demos := env("DEMOS", "orc_10")
model := env("MODEL", "4o-mini")
n := env("N", "10")
output_dir := env("OUTPUT", "output/eval_orc")
data_dir := env("DATA_DIR", "output/split")
data := env("DATA", "dev")
clean := env("CLEAN", "")

# Full data paths
data_full := root + "/"+data_dir+"/"+data+".json.zst"

eval_flags := "--demos " + demos + " -n " + n + " -m " + model +  " --papers " + data_full
output := root+"/"+output_dir+"/eval/graph/"+data+"/"+model+"/"+demos

_default:
    @echo "Environment variables:"
    @echo
    @echo "Required:"
    @echo "    DATA_DIR: source of the input data files."
    @echo
    @echo "Optional:"
    @echo "    DEMOS: see 'paper gpt demos' (default: 'orc_10')."
    @echo "    MODEL: '4o' or '4o-mini' (default: '4o-mini')."
    @echo "    N: how many items to use. Use 0 for all (default: 10)."
    @echo "    OUTPUT_DIR: where to save the outputs (default: 'output/eval_orc')."
    @echo "    DATA: which split to use; contents of DATA_DIR (default: 'dev')."
    @echo "    CLEAN: set to 1 to remove output dir without confirmation, 0 to skip cleaning,"
    @echo "           or leave undefined to prompt for confirmation."
    @echo
    @just --list --unsorted

_t msg:
    @echo '{{ style("warning") }}>>> {{msg}}{{ NORMAL }}'

# Graph evaluation
_graph prompt:
    {{eval_cmd}} graph run {{eval_flags}} --output {{output}}/graph/{{prompt}} \
    --eval-prompt {{prompt}} \
    --graph-prompt full

# Graph-eval with abstract-only prompt
[group: 'graph']
graph-sans: (_t "Running graph evaluation - sans mode")
    @just _graph sans

# Graph-eval with PETER prompt
[group: 'graph']
graph-related: (_t "Running graph evaluation - PETER (related) mode")
    @just _graph related

# Graph-eval with full graph prompt
[group: 'graph']
graph-full: (_t "Running graph evaluation - full graph mode")
    @just _graph full-graph

# Graph-eval with title and graph prompt
[group: 'graph']
graph-title: (_t "Running graph evaluation - title-only graph")
    @just _graph title-graph

# Graph-eval with only graphs prompt
[group: 'graph']
graph-only: (_t "Running graph evaluation - graphs only")
    @just _graph only-graph

# Run all graph versions
[group: 'graph']
all-graphs: (_t "Run all main graph versions and show metrics")
    @just graph-sans
    @just graph-related
    @just graph-only
    @just graph-title
    @just graph-full
    @just metrics

fluent_output := root+"/"+output_dir+"/graph-fluent/"+data+"/"+model+"/"+demos

# Graph evaluation with fluent linearisation
_graph-fluent prompt:
    {{eval_cmd}} graph run {{eval_flags}} --linearisation fluent \
        --output {{fluent_output}}/{{prompt}} \
        --eval-prompt {{prompt}}

# Graph-eval with full graph prompt
[group: 'graph-fluent']
graph-fluent-all: (_t "Running graph evaluation - fluent main graph mode")
    @just _graph-fluent full-graph

# Graph-eval with title and graph prompt
[group: 'graph-fluent']
graph-fluent-title: (_t "Running graph evaluation - fluent title-only graph")
    @just _graph-fluent title-graph

# Graph-eval with only graphs prompt
[group: 'graph-fluent']
graph-fluent-only: (_t "Running graph evaluation - fluent graphs only")
    @just _graph-fluent only-graph

# Run all fluent graph versions
[group: 'graph-fluent']
all-fluent-graphs: (_t "Run all fluent main graph versions and show metrics")
    @just graph-fluent-only
    @just graph-fluent-title
    @just graph-fluent-all
    @just metrics

# Run all unique experiments (sans/PETER/full graph)
all-standard: (_t "Comparing sans/PETER/graph variants")
    @just graph-sans
    @just graph-related
    @just graph-full

# Show metrics for all available runs
metrics:
    #!/usr/bin/env fish
    echo "Metrics for model: {{model}}, data: {{data}}, demos: {{demos}}"
    {{root}}/scripts/experiments/show_eval_results.fish {{output}}/**/metrics.json

ablation_output := root+"/"+output_dir+"/graph-ablation/"+data+"/"+model+"/"+demos

# Graph evaluation - graph component ablation
_graph-ablation eval-prompt graph-prompt:
    {{eval_cmd}} graph run {{eval_flags}} \
        --output {{ablation_output}}/g_{{graph-prompt}}-e_{{eval-prompt}} \
        --eval-prompt {{eval-prompt}} \
        --graph-prompt {{graph-prompt}}

# Graph-eval with full graph prompt
[group: 'graph-ablation']
graph-ablation-full: (_t "Running graph ablation - full graph with all components")
    @just _graph-ablation full-graph full

# Graph-eval with without experiments
[group: 'graph-ablation']
graph-ablation-noexp: (_t "Running graph ablation - full graph without experiments")
    @just _graph-ablation full-graph noexperiments

# Graph-eval with without methods
[group: 'graph-ablation']
graph-ablation-nomethod: (_t "Running graph ablation - full graph without methods")
    @just _graph-ablation full-graph nomethod

# Graph-eval with without detail
[group: 'graph-ablation']
graph-ablation-nodetail: (_t "Running graph ablation - full graph without detail")
    @just _graph-ablation full-graph nodetail

# Graph-eval with only supporting papers
[group: 'graph-ablation']
graph-ablation-positive: (_t "Running graph ablation - full graph with supporting only")
    @just _graph-ablation full-graph full-graph-positive

# Graph-eval with only contrasting papers
[group: 'graph-ablation']
graph-ablation-negative: (_t "Running graph ablation - full graph with contrasting only")
    @just _graph-ablation full-graph full-graph-negative

# All graph abalation versions
[group: 'graph-ablation']
graph-ablation-all: (_t "Running graph ablation - all variants")
    @just graph-ablation-full
    @just graph-ablation-nomethod
    @just graph-ablation-noexp
    @just graph-ablation-nodetail
    @just graph-ablation-positive
    @just graph-ablation-negative
    @just graph-ablation-metrics

# Show metrics for all available runs
[group: 'graph-ablation']
graph-ablation-metrics:
    #!/usr/bin/env fish
    echo "Ablation metrics for model: {{model}}, data: {{data}}, demos: {{demos}}"
    {{root}}/scripts/experiments/show_eval_results.fish {{ablation_output}}/**/metrics.json

peter_ablation_output := root+"/"+output_dir+"/peter-ablation/"+data+"/"+model+"/"+demos

# Cited-only PETER
_peter-cited-ablation eval-prompt graph-prompt:
    {{eval_cmd}} graph run {{eval_flags}} \
        --output {{peter_ablation_output}}/cited-g_{{graph-prompt}}-e_{{eval-prompt}} \
        --eval-prompt {{eval-prompt}} \
        --graph-prompt {{graph-prompt}} \
        --sources citations

# Semantic-only PETER
_peter-semantic-ablation eval-prompt graph-prompt:
    {{eval_cmd}} graph run {{eval_flags}} \
        --output {{peter_ablation_output}}/semantic-g_{{graph-prompt}}-e_{{eval-prompt}} \
        --eval-prompt {{eval-prompt}} \
        --graph-prompt {{graph-prompt}} \
        --sources semantic

# Graph with no PETER related papers
_peter-ablation eval-prompt graph-prompt:
    {{eval_cmd}} graph run {{eval_flags}} \
        --output {{peter_ablation_output}}/g_{{graph-prompt}}-e_{{eval-prompt}} \
        --eval-prompt {{eval-prompt}} \
        --graph-prompt {{graph-prompt}}

# Full graph with cited-only PETER
[group: 'peter-ablation']
peter-cited-full: (_t "Running full graph with cited-only PETER")
    @just _peter-cited-ablation full-graph full

# Full graph with semantic-only PETER
[group: 'peter-ablation']
peter-semantic-full: (_t "Running full graph with semantic-only PETER")
    @just _peter-semantic-ablation full-graph full

# Full graph with no PETER related papers
[group: 'peter-ablation']
peter-norel: (_t "Running full graph with no PETER papers")
    @just _peter-ablation norel-graph full

# No graph with PETER related papers
[group: 'peter-ablation']
peter-nograph: (_t "Running no graph with PETER papers")
    @just _peter-ablation related full

# Run both semantic and cited PETER ablations
[group: 'peter-ablation']
peter-ablation-all: (_t "Running PETER ablation - cited and semantic-only")
    @just peter-cited-full
    @just peter-semantic-full
    @just peter-norel
    @just peter-nograph
    @just peter-ablation-metrics

# Show metrics for PETER ablations
[group: 'peter-ablation']
peter-ablation-metrics:
    #!/usr/bin/env fish
    echo "PETER ablation metrics for model: {{model}}, data: {{data}}, demos: {{demos}}"
    {{root}}/scripts/experiments/show_eval_results.fish {{peter_ablation_output}}/**/metrics.json

rationale_data := root+"/output/eval/4o/graph/full-graph/result.json.zst"
rationale_flags := " -n " + n + " -m " + model +  " --graphs " + rationale_data
rationale_output := root+"/"+output_dir+"/eval-rationale/{{model}}"

# Run rationale evaluation on GPT 4o output
rationale:
    {{eval_cmd}} rationale run {{rationale_flags}} --output {{rationale_output}}

# Metrics for all experiments for paper output
all-metrics:
    #!/usr/bin/env fish
    echo "Metrics for model: {{model}}, data: {{data}}, demos: {{demos}}"
    {{root}}/scripts/experiments/show_eval_results.fish {{root}}/{{output_dir}}/**/metrics.json

data_scimon := root + "/"+data_dir+"/scimon/"+data+".json.zst"
scimon_flags := "--demos " + demos + " -n " + n + " -m " + model + " --ann-graph " + data_scimon

_scimon prompt:
    {{eval_cmd}} scimon run {{scimon_flags}} --output {{output}}/scimon/scimon-{{prompt}} --user-prompt {{prompt}}

# SciMON evaluation
scimon: (_t "Running SciMON evaluation")
    @just _scimon simple

# Clean the output directory based on CLEAN env var
_clean-output:
    #!/usr/bin/env fish
    set output_path "{{root}}/{{output_dir}}"

    # First check if the directory exists
    if not test -d "$output_path"
        echo "Output directory doesn't exist, nothing to clean"
        exit 0
    end

    if test "{{clean}}" = "1"
        echo "Cleaning output directory: $output_path"
        command rm -rf "$output_path"
    else if test "{{clean}}" = "0"
        echo "Skipping directory removal"
    else
        # CLEAN is undefined or has another value, prompt for confirmation
        echo "Will clean output directory: $output_path"
        read -P "Are you sure you want to remove this directory? [y/N] " confirm
        if string match -q -i "y" "$confirm"
            echo "Cleaning output directory..."
            command rm -rf "$output_path"
        else
            echo "Skipping directory removal"
        end
    end

# Run all experiments for paper output
all-experiments: (_t "Running ALL experiments")
    @just _clean-output

    @just _t "Baselines"
    @just graph-sans
    @just graph-related

    @just _t "Basic graph versions"
    # @just graph-only
    # @just graph-title
    @just graph-full

    # Fluent is really bad. It's here for archival purposes.
    # @just _t "Fluent graph versions"
    # @just graph-fluent-only
    # @just graph-fluent-title
    # @just graph-fluent-all

    @just _t "Graph ablations"
    # @just graph-ablation-nomethod
    # @just graph-ablation-noexp
    # @just graph-ablation-nodetail

    @just _t "Peter ablations"
    @just peter-cited-full
    @just peter-semantic-full
    @just peter-norel

    @just _t "SciMON"
    @just scimon

    @just all-metrics
